apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.jaredsburrows.spoon"
apply plugin: 'com.github.triplet.play'

android {
  if (file('../signing/play.json').exists()) {
    playAccountConfigs {
      release {
        jsonFile = file("../signing/play.json")
      }
    }
  }

  compileSdkVersion 28
  defaultConfig {
    applicationId "ie.pennylabs.x.basil"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 16
    versionName "0.1.$versionCode"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    archivesBaseName = "basil-$versionName"

    if (file('../signing/play.json').exists()) {
      playAccountConfig = playAccountConfigs.release
    }
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  sourceSets {
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  signingConfigs {
    if (file('../signing/release.keystore').exists()) {
      release {
        storeFile file(Keystore.RELEASE_STORE)
        storePassword Keystore.STORE_PASSWORD
        keyAlias Keystore.KEY_ALIAS
        keyPassword Keystore.KEY_PASSWORD
      }
    }

    debug {
      storeFile file('../signing/debug.keystore')
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix '.debug'
    }

    release {
      if (file('../signing/release.keystore').exists()) {
        signingConfig signingConfigs.release
      } else {
        signingConfig signingConfigs.debug
      }
      postprocessing {
        proguardFiles 'proguard-rules.pro'
        removeUnusedResources = true
        removeUnusedCode = true
        optimizeCode = true
        obfuscate = false
        // https://github.com/material-components/material-components-android/issues/115
      }
    }
  }

  testOptions {
    animationsDisabled = true
  }
}

play {
  track = 'internal'
  untrackOld = true
  uploadImages = true
}

spoon {
  baseOutputDir = "$project.buildDir/reports/androidTests/spoon"
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Deps.kotlin"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${Deps.coroutines}"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:${Deps.coroutines}"

  implementation 'com.google.android.material:material:1.0.0-alpha1'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.0'
  implementation "androidx.lifecycle:lifecycle-livedata:2.0.0-alpha1"
  implementation "androidx.room:room-runtime:$Deps.room"
  kapt "androidx.room:room-compiler:$Deps.room"

  implementation 'com.jakewharton.timber:timber:4.7.0'
  implementation 'com.squareup.moshi:moshi-kotlin:1.6.0'
  implementation "com.google.dagger:dagger:$Deps.dagger"
  implementation "com.google.dagger:dagger-android:$Deps.dagger"
  kapt "com.google.dagger:dagger-compiler:$Deps.dagger"
  kapt "com.google.dagger:dagger-android-processor:$Deps.dagger"

  testImplementation 'junit:junit:4.12'
  androidTestImplementation "androidx.test:rules:$Deps.androidx_test"
  androidTestImplementation "androidx.test:runner:$Deps.androidx_test"
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3', {
    exclude group: 'androidx.annotations', module: 'annotations'
  }
  androidTestImplementation "com.jraska:falcon:$Deps.falcon"
  androidTestImplementation "com.jraska:falcon-spoon-compat:$Deps.falcon"
  androidTestImplementation "com.squareup.spoon:spoon-client:2.0.0-SNAPSHOT"
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}
androidExtensions {
  experimental = true
}

check.dependsOn rootProject.detektCheck

task prepareNextRelease {
  description "Prepares the project for the next release version, see: RELEASING.md"

  doLast {
    def new_version = project.android.defaultConfig.versionName

    // 7. Create a new release branch `git checkout -b release-{versionName}`
    exec { commandLine 'git', 'checkout', '-b', "release-$new_version" }

    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = stdout
    }
    exec {
      def short_hash = stdout.toString().take(7) // make sure there's no unwanted chars

      // 8. Update previous `CHANGELOG.md` entry to append title with the build commit of that release
      commandLine 'sed', '-i', "", '-e', "1 s/\$/ - $short_hash/", file('../CHANGELOG.md')
    }

    // 9. Commit changes `git commit -am 'bump version to {versionName}'`
    exec { commandLine 'git', 'commit', '-am', "bump version to $new_version" }
  }
}